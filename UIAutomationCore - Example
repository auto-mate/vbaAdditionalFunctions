Sub ClickCutePDFSave()   

 Dim Auto As New CUIAutomation    
 Dim Dsktp As IUIAutomationElement    
 Dim DtChildNodes As IUIAutomationElementArray    
 Dim AppChildNodes As IUIAutomationElementArray    
 Dim ButtonList As IUIAutomationElementArray    
 Dim INV As IUIAutomationInvokePattern        
 
 '// Get Desktop Element    
 '//####################    
 Set Dsktp = Auto.GetRootElement            
 
 '// Get Desktops Children    
 '//######################    
 Set DtChildNodes = Dsktp.FindAll(TreeScope_Children, Auto.CreateTrueCondition)        
 
 '// Loop through Desktops Children to find CutePDF    
 '//###############################################    
 For N = 0 To (DtChildNodes.Length - 1)        
   If InStr(1, DtChildNodes.GetElement(N).CurrentName, "CutePDF") <> 0 Then Exit For    
 Next            
 
 '// Get CutePDF's Children    
 '//#######################     
 Set AppChildNodes = DtChildNodes.GetElement(N).FindAll(TreeScope_Children, Auto.CreateTrueCondition)   
 
 '// Loop through CutePDF Children to find types ToolBarWindow32    
 '//############################################################  
  For j = 0 To (AppChildNodes.Length - 1)         
    If AppChildNodes.GetElement(j).CurrentClassName = "ToolbarWindow32"   Then    
      '//Get Current Button List For Found ToolBar     
      '//#########################################         
      Set ButtonList = AppChildNodes.GetElement(j).FindAll(TreeScope_Children, Auto.CreateTrueCondition)    
      '//Loop through Button List of Found ToolBar   
      '//#########################################  
      tbFound = False         
      For K = 0 To (ButtonList.Length - 1)   
        '//If Toolbar Item has name of open this is the correct toolbar i.e. Set tbFound = True  
        '//####################################################################################     
        If ButtonList.GetElement(K).CurrentName = "Open" Then tbFound = True        
        '//Button Ref 0 = Exit, 1=Open 2=Save [Look at Application toolbar to see why 2 is chosen]  
        '//#######################################################################################    
        If K = 2 And tbFound Then          
          '//Create The Required Pattern Object "Invoke" as INV for this element     
          '//###################################################################    
          Set INV = ButtonList.GetElement(K).GetCurrentPattern(UIA_PatternIds.UIA_InvokePatternId)  
          '//Use the "Invoke" property of the Pattern Object INV to call the button press           
          '//############################################################################         
          INV.Invoke            
        End If         
      Next      
    End If 
  Next    
  
  Set Button = Nothing  
  Set ButtonList = Nothing    
  Set DtChildNodes = Nothing  
  Set AppChildNodes = Nothing  
  Set Dsktp = Nothing 
  Set Auto = Nothing
  
  End Sub
